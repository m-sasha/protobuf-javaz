buildscript{

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies{
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }

}

plugins{
    id 'java'
    id 'com.google.protobuf' version '0.8.5'
}

group 'com.maryanovsky.protobuf-javaz'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories{
    mavenCentral()
}



dependencies{
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: '3.5.1'
    compile 'com.squareup:javapoet:1.11.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


// Distribution directory
def distDir = file("$buildDir/dist")


// Create an executable jar
jar {
    destinationDir = distDir
    archivesBaseName = 'protobuf-javaz-gen'
    manifest {
        attributes 'Main-Class': 'com.maryanovsky.pbjz.gen.ProtobufJavaZero'
        attributes 'Class-Path' : configurations.runtime.collect { it.getName() }.join(' ')
    }
}


// Copy dependency jars
task copyRuntimeLibs(type: Copy){
    into distDir
    from configurations.runtime
}


// Create executable script, which can be used as a protoc plugin
task createExecutable{
    doLast{
        File script = new File(distDir, "protobuf-javaz-gen")
        script.text = """#!/bin/bash
java -jar protobuf-javaz-gen-1.0-SNAPSHOT.jar
"""
        script.setExecutable(true, true)
    }
}

// Distribution task
task dist(dependsOn: [build, jar, copyRuntimeLibs, createExecutable]){}